// Add Gradle FatJar Plugin, see https://github.com/musketyr/gradle-fatjar-plugin
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.2-rc1'
    }
}

subprojects {
	apply plugin: 'fatjar'
	apply plugin: 'application'

	project.ext.dropWizardConfig = 'src/etc/config.yml'

	dependencies {
	    compile project(':server:configuracao')
	    compile project(':server:database')
		compile project(':server:discovery')
	    compile project(':server:integracao')
		compile project(':server:monitoring')
	    compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
	    compile group: 'com.yammer.dropwizard', name: 'dropwizard-client', version: dropwizardVersion
	    compile group: 'com.yammer.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
	    compile group: 'com.yammer.dropwizard', name: 'dropwizard-jdbi', version: dropwizardVersion
	    compile group: 'com.netflix.hystrix', name: 'hystrix-core', version: '1.2.16'
	    runtime group: 'org.postgresql', name: 'postgresql', version: '9.2-1002-jdbc4'
	    testCompile group: 'junit', name: 'junit', version: '4.11'
	}

	sourceSets {
	    main {
			resources {
				srcDir 'src/etc'
			}
		}
	}

	fatJar {
	    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
	    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
	}

	// Configure the run task to start the Dropwizard service
	run {
	    args 'run', dropWizardConfig
	}

	task wrapper(type: Wrapper) {
	    gradleVersion = '1.6'
	}
}
